fun RepositoryHandler.github(repository: String) {
    maven("https://maven.pkg.github.com/$repository") {
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

val hotlibsKatalogVersionProvider = providers.gradleProperty("hotlibsKatalogVersion")

dependencyResolutionManagement {
    @Suppress("UnstableApiUsage")
    repositories {
        mavenCentral()
        maven("https://packages.confluent.io/maven/")
        github("navikt/hotlibs")

        // Plassert under GitHub-repositories (med authentication) for å unngå unødvendige kostnader.
        maven("https://github-package-registry-mirror.gc.nav.no/cached/maven-release")
    }
    versionCatalogs {
        create("libs") {
            from(hotlibsKatalogVersionProvider.map { "no.nav.hjelpemidler:katalog:$it" }.get())
        }
    }
}

rootProject.name = "hm-personhendelse"
